//@version=2
// John Ehlers' Cyber Cycle Period from his book
// "Cybernetics Analysis for Stock and Futures"
// implementation based on original code from LazyBear 
study("John Ehlers' Cyber Cycle Period", shorttitle="ECP", overlay=false, precision=3)

src=input(hl2, title="Source") 
a=input(.07, title="Alpha")

// smooth price data
s = (src + 2*src[1] + 2*src[2] + src[3])/6.0

// prepare cycle
c = s[0]
c := n<7 ? (src - 2*src[1] + src[2])/4.0 : ((1 - 0.5*a)*(1 - 0.5*a)*(s - 2*s[1] + s[2]) + 2*(1-a)* nz(c[1]) - (1 - a)*(1-a)* nz(c[2]))

// 
q1 = (.0962*c + 0.5769*c[2] - 0.5769*c[4] - .0962*c[6])*(0.5+.08*nz(ip[1]))
I1 = c[3]
dp_ = iff(q1 != 0 and q1[1] != 0, (I1/q1 - I1[1]/q1[1]) / (1 + I1*I1[1]/(q1*q1[1])),0.0)
dp = iff(dp_ < 0.1, 0.1, iff(dp_ > 0.9, 0.9, dp_))

med(x,y,z) => (x+y+z) - min(x,min(y,z)) - max(x,max(y,z))
md = med(dp,dp[1], med(dp[2], dp[3], dp[4]))

dc = iff(md == 0, 15, 6.28318 / md + 0.5)
ip = .33*dc + .67*nz(ip[1])
p = .15*ip + .85*nz(p[1])

plot(round(p), title="Cycle Period",color=blue)