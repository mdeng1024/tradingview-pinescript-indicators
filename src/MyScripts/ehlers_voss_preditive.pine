// A Peek Into the Furure
// John F. Ehlers
// TASC Aug 2019
// http://traders.com/Documentation/FEEDbk_docs/2019/08/TradersTips.html
// Created by e2e4mfck for tradingview.com

// Design to help signal cyclical turning points in the markets

//@version=4

title = "Voss Predictive Filter"
version = "1.1.0"

study (title=title+version, overlay=false)

// - - - - - Inputs - - - - - //
Source	  = input(close, type=input.source)
Period    = input(20, type=input.integer)
Predict   = input(3, type=input.integer)
Bandwidth = input(0.25, type=input.float)

// initializing variables
PI	  = 3.1415926
Order = 3*Predict
F1 	  = cos(2*PI/Period)
G1    = cos(Bandwidth*2*PI/Period)
S1    = 1/G1 - sqrt(1/(G1*G1)-1)

S2    = 1+S1
S3    = 1-S1
X1    = Source-Source[2]
X2    = (3+Order)/2


// - - - - - Filt - - - - - //
float Filt = 0
Filt := bar_index <= 5? 0: 0.5*S3*X1 + F1*S2*Filt[1] - S1*Filt[2]
	

// - - - - - Voss - - - - - //
float SumC = 0
float Voss = 0
for i = 0 to (Order - 1)
	SumC := SumC + ((i+1)/Order) * Voss[Order-i]  // * Voss[Order-i-1]

Voss := bar_index <= Order? 0 : X2*Filt-SumC

// Compute Root Mean Square
float rms = sqrt(sum(Voss * Voss, Period) / Period)

// - - - - - Plot  - - - - - //
plot(rms, title="+RMS", color=#f6b26b, transp=0)
plot(Voss ,   title="Voss" , style=plot.style_line , color=color.blue , linewidth=1)
hline(0.0 ,   title="Zero" , linestyle=hline.style_dashed , color=color.black , linewidth=1)
plot(Filt ,   title="Filter" , style=plot.style_line , color=color.red  , linewidth=1)
plot(-rms, title="-RMS", color=#f6b26b, transp=0)


// Suggested usage:
//  Long when Voss cross above Filt, 
//  Short when Voss cross below Filt
//