//@version=3
// original source code by jwammo12
study("Adaptive Bandpass Trigger Oscillator")

Delta = input(defval=0.75, title="Delta")
Alpha = input(.05)
Lookback = input(1000)
Overbought = input(80)
Oversold = input(20)
PeriodMultiplier = input(1.0)

a = Alpha
src =  hl2


Pi = 3.141592654

ip = 0.0
s = (src + 2*src[1] + 2*src[2] + src[3])/6.0
c = 0.0
c := n<7?(src - 2*src[1] + src[2])/4.0:((1 - 0.5*a)*(1 - 0.5*a)*(s - 2*s[1] + s[2]) + 2*(1-a)*c[1] - (1 - a)*(1-a)*c[2])
q1 = (.0962*c + 0.5769*c[2] - 0.5769*c[4] - .0962*c[6])*(0.5+.08*nz(ip[1]))
I1 = c[3]
dp_ = iff(q1 != 0 and q1[1] != 0, (I1/q1 - I1[1]/q1[1]) / (1 + I1*I1[1]/(q1*q1[1])),0)
dp = iff(dp_ < 0.1, 0.1, iff(dp_ > 1.1, 1.1, dp_))
med(x,y,z) => (x+y+z) - min(x,min(y,z)) - max(x,max(y,z))
md = med(dp,dp[1], med(dp[2], dp[3], dp[4]))
dc = iff(md == 0, 15, 6.28318 / md + 0.5)

ip := .33*dc + .67*nz(ip[1])
p = 0.0
p := .15*ip + .85*nz(p[1])

length = p * PeriodMultiplier

hline(50, color=blue, linestyle=dotted)
hline(Overbought, color=white, linestyle=dotted)
hline(Oversold, color=white, linestyle=dotted)

beta = cos(Pi * (360 / length) / 180)
gamma = 1 / cos(Pi * (720 * Delta / length) / 180)
alpha = gamma - sqrt(gamma * gamma - 1)
// two-pole band-pass filter
BP = 0.0
BP := 0.5 * (1 - alpha) * (src - src[2]) + beta * (1 + alpha) * nz(BP[1]) - alpha * nz(BP[2])

q = (BP -BP[1])*length/(2.0*Pi)

lead = .5*BP + .866*q

leadPR = percentrank(lead, Lookback)
BPPR = percentrank(BP, Lookback)

plot(BPPR, color=red, title="In Phase")
plot(leadPR, color=green, title="Lead")